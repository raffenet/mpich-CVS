dnl
dnl Caching is usually WRONG for systems with cross-mounted file systems
dnl (the cache file may correspond to a different system).  Since configure
dnl is not on a performance-critical path, go for robustness over speed.
dnl
define([AC_CACHE_LOAD], )dnl
define([AC_CACHE_SAVE], )dnl
dnl
dnl
AC_INIT()
AC_PREREQ(2.52)
AC_CONFIG_HEADER(mpe_collchk_conf.h)
dnl
echo "Configuring MPE MPI-Collective Checking Library with $ac_configure_args"
dnl
dnl Set top_srcdir_abs to be the ABSOLUTE path to the home directory of MPE
dnl top_srcdir is in relative path.
if test "X$srcdir" != "X." -a -s $srcdir/include/collchk.h ; then
    top_srcdir_abs=$srcdir
else
    PAC_GETWD( top_srcdir_abs, include/collchk.h )
fi
AC_SUBST(top_srcdir_abs)

AC_ARG_ENABLE( echo,
[--enable-echo                      - Turn on strong echoing.
                                     The default is enable=no.],
set -x )

AC_ARG_ENABLE( softerror,
[--enable-softerror                 - Turn on soft error, i.e. instead of
                                     issuing MSG_ERROR, MSG_WARN + exit 0
                                     are used.  The default is enable=no.],
, enable_softerror=${mpe_enable_softerror:-no} )

MAKE=${MAKE:-make}
CC=${CC:-cc}
CLINKER=${CLINKER:-$CC}
CFLAGS=${MPE_CFLAGS}
MPI_CC=${MPI_CC:-${CC}}
MPI_INC=${MPI_INC}
MPI_LIBS=${MPI_LIBS}

MPE_LIBNAME=${MPE_LIBNAME:-mpe}
COLLCHK_LIBNAME="${MPE_LIBNAME}_collchk"
COLLCHK_LIBS="-l$COLLCHK_LIBNAME"

dnl Fixup for make
PAC_MAKE_IS_GNUMAKE
PAC_MAKE_IS_BSD44
PAC_MAKE_IS_OSF
PAC_MAKE_VPATH
AC_SUBST(MAKE)

dnl If we are relying on vpath and no vpath is set, then we must exit
if test ! -s src/log_mpi_core.c -a -z "$VPATH" ; then
    AC_MSG_ERROR( [No virtual MAKE path command found.
        You may need to set your make command
        The GNU make (sometimes available as gnumake) can be used.] )
fi

dnl CPRP is the version of cp that accepts -r and -p arguments.
MKDIR=mkdir
CPRP=cp
RM=rm
MV=mv
AC_SUBST(MKDIR)
AC_SUBST(CPRP)
AC_SUBST(RM)
AC_SUBST(MV)

AC_ARG_WITH( mpiio,
[--with-mpiio                       - Include/exclude MPI-IO from Verification.
                                     The default is yes.], ,
with_mpiio=yes )

AC_ARG_ENABLE( checkMPI,
[--enable-checkMPI                  - Turn on/off the linking test for the MPI
                                     include files, MPI libraries and MPI_CC
                                     compiler. The default is enable=yes.], ,
enable_checkMPI=yes )

AC_ARG_WITH( checkMPIO,
[--enable-checkMPIO                 - Turn on/off the linking test for MPI-IO
                                     routines in MPI implementation.
                                     The default is enable=yes.], ,
enable_checkMPIO=yes )

if test -z "$nonopt" ; then nonopt=NONE ; fi
AC_CANONICAL_HOST

dnl Set the various build directories from their mpe_ prefixed env variables.
dnl
dnl includebuild_dir is for all user header files
dnl libbuild_dir is used to build the libraries in before they are installed.
dnl binbuild_dir is for the scripts/programs
dnl sbinbuild_dir is for all system admin stuffs
dnl
rootbuild_dir=`pwd`
for dir in include lib bin ; do
    dirname=${dir}build_dir
    mpe_dirname=mpe_${dirname}
    eval dirvalue=\$"$dirname"
    eval $dirname=\$\{${mpe_dirname}\}
done

for dir in include lib bin ; do
    dirname=${dir}build_dir
    eval dirvalue=\$"$dirname"
    if test -n "$dirvalue" ; then
        if test ! -d $dirvalue ; then
            if mkdir -p $dirvalue ; then
                :
            else
                PAC_MSG_ERROR( $enable_softerror,
                               [Could not create directory $dirvalue] )
            fi
        fi
    fi
done

AC_SUBST(includebuild_dir)
AC_SUBST(libbuild_dir)
AC_SUBST(binbuild_dir)

AC_SUBST(includebuild_dir)
AC_SUBST(libbuild_dir)
AC_SUBST(binbuild_dir)

AC_PROG_CC
AC_OBJEXT
AC_EXEEXT
dnl Check for broken handling of common symbols
dnl PAC_PROG_C_BROKEN_COMMON
AC_CHECK_PROG(AR, ar, ar, ;)
AC_PROG_RANLIB

dnl We need to check that this has worked.  The autoconf macro is broken
AC_PROG_CPP
if test "$CPP" = "/lib/cpp" -a ! -x /lib/cpp ; then
   AC_MSG_ERROR( [configure could not find a working C preprocessor] )
fi

dnl  Invoking AC_CHECK_LIB before AC_PROG_CC makes AC_PROG_CC fail
dnl  in autoconf 2.59
DEBUG_LIBS=""
if test "$enable_debugcheck" = "yes" ; then
    AC_CHECK_LIB( efence, malloc, DEBUG_LIBS="-lefence" )
fi
AC_SUBST(DEBUG_LIBS)

AC_HEADER_STDC
AC_C_CONST

dnl  Checking Headers
AC_CHECK_HEADERS( stdio.h stdlib.h string.h unistd.h )

AC_FUNC_ALLOCA

dnl Check MPI_Add_error_class, MPI_Add_error_code, MPI_Add_error_string
dnl and MPI_Comm_call_errhandler
if test "$MPI_IMPL" != "MPICH" -a "$MPI_IMPL" != "MPICH2" ; then
    if test "$enable_checkMPI" = "yes" ; then
        AC_MSG_CHECKING( [for MPI Error functions] )
        PAC_MPI_LINK_CC_FUNC( $MPI_CC, [$CFLAGS $MPI_INC], $MPI_LIBS,
                              [
    int itmp, err_class, err_code;
    char err_str[]="test";
                              ],
                              [
    itmp=MPI_Add_error_class( &err_class );
    itmp=MPI_Add_error_code( err_class, &err_code );
    itmp=MPI_Add_error_string( err_code, err_str );
    itmp=MPI_Comm_call_errhandler( MPI_COMM_WORLD, err_code );
                              ],
                              [ mpe_link_ok=yes ], [ mpe_link_ok=no ] )
        if test "$mpe_link_ok" = "yes" ; then
            AC_MSG_RESULT(yes)
            AC_DEFINE( HAVE_MPI_ERR_FNS, 1,
                       [Define if MPI Error functions exist] )
        else
            AC_MSG_RESULT(no)
        fi
    fi
else
dnl if MPICH2, assume MPI_IN_PLACE is available, not in MPICH1
    if test "$MPI_IMPL" = "MPICH2" ; then
        AC_DEFINE( HAVE_MPI_ERR_FNS, 1, [Define if MPI Error functions exist] )
    fi
fi


dnl Check MPI_IN_PLACE
if test "$MPI_IMPL" != "MPICH" -a "$MPI_IMPL" != "MPICH2" ; then
    if test "$enable_checkMPI" = "yes" ; then
        AC_MSG_CHECKING( [for MPI_IN_PLACE] )
        PAC_MPI_LINK_CC_FUNC( $MPI_CC, [$CFLAGS $MPI_INC], $MPI_LIBS,
                              [ int itmp, iboolean; ],
                              [
    itmp=MPI_Allreduce( MPI_IN_PLACE, &iboolean, 1, MPI_INT,
                        MPI_LAND, MPI_COMM_WORLD )
                              ],
                              [ mpe_link_ok=yes ], [ mpe_link_ok=no ] )
        if test "$mpe_link_ok" = "yes" ; then
            AC_MSG_RESULT(yes)
            AC_DEFINE( HAVE_MPI_IN_PLACE, 1, [Define if MPI_IN_PLACE exists] )
        else
            AC_MSG_RESULT(no)
        fi
    fi
else
dnl if MPICH2, assume MPI_IN_PLACE is available, not in MPICH1
    if test "$MPI_IMPL" = "MPICH2" ; then
        AC_DEFINE( HAVE_MPI_IN_PLACE, 1, [Define if MPI_IN_PLACE exists] )
    fi
fi

dnl Check MPI_SIGNED_CHAR
if test "$MPI_IMPL" != "MPICH" -a "$MPI_IMPL" != "MPICH2" ; then
    if test "$enable_checkMPI" = "yes" ; then
        AC_MSG_CHECKING( [for MPI_SIGNED_CHAR] )
        PAC_MPI_LINK_CC_FUNC( $MPI_CC, [$CFLAGS $MPI_INC], $MPI_LIBS,
                              [ MPI_Datatype type; ],
                              [ type = MPI_SIGNED_CHAR ],
                              [ mpe_link_ok=yes ], [ mpe_link_ok=no ] )
        if test "$mpe_link_ok" = "yes" ; then
            AC_MSG_RESULT(yes)
            AC_DEFINE( HAVE_MPI_SIGNED_CHAR, 1,
                       [Define if MPI_SIGNED_CHAR exists] )
        else
            AC_MSG_RESULT(no)
        fi
    fi
else
dnl if MPICH2, assume MPI_SIGNED_CHAR is available, not in MPICH1
    if test "$MPI_IMPL" = "MPICH2" ; then
        AC_DEFINE( HAVE_MPI_SIGNED_CHAR, 1,
                   [Define if MPI_SIGNED_CHAR exists] )
    fi
fi

dnl Check Fortran MPI_Datatype definition in C
if test "$MPI_IMPL" != "MPICH" -a "$MPI_IMPL" != "MPICH2" ; then
    if test "$enable_checkMPI" = "yes" ; then
        AC_MSG_CHECKING( [for Fortran MPI_Datatype in C] )
        PAC_MPI_LINK_CC_FUNC( $MPI_CC, [$CFLAGS $MPI_INC], $MPI_LIBS,
                              [ MPI_Datatype type ],
                              [
    type = MPI_COMPLEX;
                              ],
                              [ mpe_link_ok=yes ], [ mpe_link_ok=no ] )
        if test "$mpe_link_ok" = "yes" ; then
            AC_MSG_RESULT(yes)
            AC_DEFINE( HAVE_FORTRAN_MPI_DATATYPE_IN_C, 1,
                       [Define if Fortran MPI_Datatype exists in C] )
        else
            AC_MSG_RESULT(no)
            AC_MSG_CHECKING( [for LAM/MPI Fortran MPI_Datatype in C] )
            PAC_MPI_LINK_CC_FUNC( $MPI_CC, [$CFLAGS $MPI_INC], $MPI_LIBS,
                                  [ MPI_Datatype type ],
                                  [
#define MPI_COMPLEX   ((MPI_Datatype) &lam_mpi_cplex)
    type = MPI_COMPLEX;
                                  ],
                                  [ mpe_link_ok=yes ], [ mpe_link_ok=no ] )
            if test "$mpe_link_ok" = "yes" ; then
                AC_MSG_RESULT(yes)
                AC_DEFINE( HAVE_LAM_FORTRAN_MPI_DATATYPE_IN_C, 1,
                           [Define if LAM Fortran MPI_Datatype exists in C] )
            else
                AC_MSG_RESULT(no)
            fi
        fi
    fi
else
dnl if MPICH2, assume Fortran MPI_Datatypes are available in C, same for MPICH1.
    if test "$MPI_IMPL" = "MPICH2" ; then
       AC_DEFINE( HAVE_FORTRAN_MPI_DATATYPE_IN_C, 1,
                  [Define if Fortran MPI_Datatype exists in C] )
    else
       AC_DEFINE( HAVE_FORTRAN_MPI_DATATYPE_IN_C, 1,
                  [Define if Fortran MPI_Datatype exists in C] )
    fi
fi


dnl Check MPI-IO
if test "$MPI_IMPL" != "MPICH" -a "$MPI_IMPL" != "MPICH2" ; then
    if test "$enable_checkMPI" = "yes" -a "$enable_checkMPIO" = "yes" ; then
        AC_MSG_CHECKING( [for the C version of MPI_File_open()] )
        PAC_MPI_LINK_CC_FUNC( $MPI_CC, [$CFLAGS $MPI_INC], $MPI_LIBS,
                              [ int itmp; MPI_File fh ],
                              [ itmp=MPI_File_open( MPI_COMM_WORLD, "mpiiotest", MPI_MODE_CREATE | MPI_MODE_RDWR, MPI_INFO_NULL, &fh ) ],
                              [ mpe_link_ok=yes ], [ mpe_link_ok=no ] )
        if test "$mpe_link_ok" = "yes" ; then
            AC_MSG_RESULT(yes)
            AC_MSG_CHECKING( [for the C version of PMPI_File_open()] )
            PAC_MPI_LINK_CC_FUNC( $MPI_CC, [$CFLAGS $MPI_INC], $MPI_LIBS,
                                  [ int itmp; MPI_File fh ],
                                  [ itmp=PMPI_File_open( MPI_COMM_WORLD, "mpiiotest", MPI_MODE_CREATE | MPI_MODE_RDWR, MPI_INFO_NULL, &fh ) ],
                                  [ mpe_link_ok=yes ], [ mpe_link_ok=no ] )
            if test "$mpe_link_ok" = "yes" ; then
                AC_MSG_RESULT(yes)
                AC_MSG_CHECKING( [for ROMIO's MPIO_Request] )
                PAC_MPI_LINK_CC_FUNC( $MPI_CC, [$CFLAGS $MPI_INC], $MPI_LIBS,
                                      [ MPIO_Request  io_request], ,
                                      [ mpe_link_ok=yes ], [ mpe_link_ok=no ] )
                if test "$mpe_link_ok" = "yes" ; then
                    AC_MSG_RESULT(yes)
                    COLLCHK_BUILD_IO=yes
                else
                    AC_MSG_RESULT(no)
                    COLLCHK_BUILD_IO=no
                fi
            else
                AC_MSG_RESULT([no, assumed No MPI-IO routines])
                COLLCHK_BUILD_IO=no
            fi
        else
            AC_MSG_RESULT([no, assumed No MPI-IO routines])
            COLLCHK_BUILD_IO=no
        fi
    fi
else
dnl When it is built with MPICHx, MPE configure observes --with[out]-mpiio
dnl if test \( "$MPI_IMPL" = "MPICH" -o "$MPI_IMPL" = "MPICH2" \) \
    if test "$with_mpiio" = "yes" ; then
        COLLCHK_BUILD_IO=yes
    else
        COLLCHK_BUILD_IO=no
    fi
fi

dnl Check MPI-RMA
if test "$MPI_IMPL" != "MPICH" -a "$MPI_IMPL" != "MPICH2" ; then
    if test "$enable_checkMPI" = "yes" ; then
        AC_MSG_CHECKING( [for the C version of MPI_Win_create] )
        PAC_MPI_LINK_CC_FUNC( $MPI_CC, [$CFLAGS $MPI_INC], $MPI_LIBS,
                              [ int itmp; MPI_Win win ],
                              [
#ifndef NULL
#define NULL 0
#endif
    itmp=MPI_Win_create( NULL, 0, 1, MPI_INFO_NULL, MPI_COMM_WORLD, &win )
                              ],
                              [ mpe_link_ok=yes ], [ mpe_link_ok=no ] )
        if test "$mpe_link_ok" = "yes" ; then
            AC_MSG_RESULT(yes)
            AC_MSG_CHECKING( [for the C version of PMPI_Win_create] )
            PAC_MPI_LINK_CC_FUNC( $MPI_CC, [$CFLAGS $MPI_INC], $MPI_LIBS,
                                  [ int itmp; MPI_Win win ],
                                  [
#ifndef NULL
#define NULL 0
#endif
    itmp=PMPI_Win_create( NULL, 0, 1, MPI_INFO_NULL, MPI_COMM_WORLD, &win )
                                  ],
                                  [ mpe_link_ok=yes ], [ mpe_link_ok=no ] )
            if test "$mpe_link_ok" = "yes" ; then
                AC_MSG_RESULT(yes)
                COLLCHK_BUILD_RMA=yes
            else
                AC_MSG_RESULT([no, assumed No MPI-RMA routines])
                COLLCHK_BUILD_RMA=no
            fi
        else
            AC_MSG_RESULT([no, assumed No MPI-RMA routines])
            COLLCHK_BUILD_RMA=no
        fi
    fi
else
dnl if MPICH2, assume MPI-RMA is available.
    if test "$MPI_IMPL" = "MPICH2" ; then
        COLLCHK_BUILD_RMA=yes
     else
        COLLCHK_BUILD_RMA=no
    fi
fi

dnl Check MPI-COMMS
if test "$MPI_IMPL" != "MPICH" -a "$MPI_IMPL" != "MPICH2" ; then
    if test "$enable_checkMPI" = "yes" ; then
        AC_MSG_CHECKING( [for the C version of MPI_Comm_spawn] )
        PAC_MPI_LINK_CC_FUNC( $MPI_CC, [$CFLAGS $MPI_INC], $MPI_LIBS,
                              [ MPI_Comm intercomm;
                                int itmp, np=1, ierrs; ],
                              [
#ifndef NULL
#define NULL 0
#endif
    itmp=MPI_Comm_spawn( "./test", MPI_ARGV_NULL, np, MPI_INFO_NULL, 0,
                         MPI_COMM_WORLD, &intercomm, &ierrs )
                              ],
                              [ mpe_link_ok=yes ], [ mpe_link_ok=no ] )
        if test "$mpe_link_ok" = "yes" ; then
            AC_MSG_RESULT(yes)
            AC_MSG_CHECKING( [for the C version of PMPI_Comm_spawn] )
            PAC_MPI_LINK_CC_FUNC( $MPI_CC, [$CFLAGS $MPI_INC], $MPI_LIBS,
                                  [ MPI_Comm intercomm;
                                    int itmp, np=1, ierrs; ],
                                  [
#ifndef NULL
#define NULL 0
#endif
    itmp=PMPI_Comm_spawn( "./test", MPI_ARGV_NULL, np, MPI_INFO_NULL, 0,
                          MPI_COMM_WORLD, &intercomm, &ierrs )
                                  ],
                                  [ mpe_link_ok=yes ], [ mpe_link_ok=no ] )
            if test "$mpe_link_ok" = "yes" ; then
                AC_MSG_RESULT(yes)
                COLLCHK_BUILD_COMMS=yes
            else
                AC_MSG_RESULT([no, assumed No MPI-COMMS routines])
                COLLCHK_BUILD_COMMS=no
            fi
        else
            AC_MSG_RESULT([no, assumed No MPI-COMMS routines])
            COLLCHK_BUILD_COMMS=no
        fi
    fi
else
dnl if MPICH2, assume MPI_COMMS is available.
    if test "$MPI_IMPL" = "MPICH2" ; then
        COLLCHK_BUILD_COMMS=yes
     else
        COLLCHK_BUILD_COMMS=no
    fi
fi

AC_SUBST(COLLCHK_BUILD_IO)
AC_SUBST(COLLCHK_BUILD_RMA)
AC_SUBST(COLLCHK_BUILD_COMMS)

AC_SUBST(MPI_IMPL)
AC_SUBST(CC)
AC_SUBST(CLINKER)
AC_SUBST(CFLAGS)
AC_SUBST(MPI_CC)
AC_SUBST(MPI_INC)
AC_SUBST(MPI_LIBS)

AC_SUBST(COLLCHK_LIBNAME)
AC_SUBST(COLLCHK_LIBS)

AC_OUTPUT( Makefile src/Makefile test/Makefile )
