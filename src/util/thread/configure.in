AC_INIT(mpe_thread.h.in)

dnl
dnl Set the directory that contains support scripts such as install-sh and
dnl config.guess
dnl
AC_CONFIG_AUX_DIR(../../../confdb)

dnl
dnl Definitions will be placed in this file rather than in the DEFS variable
dnl
AC_CONFIG_HEADER(mpe_thread_conf.h)


echo "RUNNING CONFIGURE FOR THE MPE THREAD INTERFACE"

dnl
dnl First check that we have a clean build if we are doing a VPATH build
dnl
if test "`cd $srcdir && pwd`" != "`pwd`" && \
   test -f $srcdir/config.status ; then
    AC_MSG_ERROR([You cannot do a VPATH build if the source directory has been
    configured.  Run "make distclean" in $srcdir first.])
fi

MPE_THREAD_DEFAULT=${MPE_THREAD_DEFAULT-posix}

AC_ARG_WITH(thread-package,
[--with-thread-package=package - Thread package to use.  Supported thread
packages include:
    posix or pthreads - POSIX threads
    solaris - Solaris threads (Solaris OS only)
    none - no threads
If the option is not specified, the default package is ${MPE_THREAD_DEFAULT}.
If the option is specified, but a package is not given, then the default
is posix.],,
with_thread_package=${MPE_THREAD_DEFAULT})

if test "$with_thread_package" = "yes" ; then
    with_thread_package=posix
fi

PAC_PROG_MAKE
AC_PROG_INSTALL
PAC_PROG_CHECK_INSTALL_WORKS
PAC_PROG_MKDIR_P

AC_SUBST(AR)
AC_SUBST(RANLIB)
AC_SUBST(MPILIBNAME)
AC_SUBST(CC)
AC_SUBST(DEFS)
AC_SUBST(CFLAGS)
AC_SUBST(CPPFLAGS)
PAC_CC_SUBDIR_SHLIBS
AC_SUBST(master_top_srcdir)
AC_SUBST(master_top_builddir)
dnl
dnl Dependency handling
AC_SUBST(MAKE_DEPEND_C)

PAC_SUBDIR_CACHE

case $with_thread_package in 
    posix|pthreads)
	with_thread_package=posix
	AC_CHECK_HEADERS(pthread.h)
	AC_CHECK_FUNCS(pthread_yield)
	# OSF1 has __pthread_create but not pthread_create (because of 
	# inconsistencies in the pthread spec).  Thus, we look for pthread_key_create
	AC_SEARCH_LIBS(pthread_key_create,pthread,found=yes,found=no)
	if test "$found" != "yes" ; then
	   AC_MSG_ERROR([unable to find pthreads library])
	fi
	THR_LIBS="$THR_LIBS -lpthread"
	;;
    solaris)
	AC_CHECK_HEADERS(thread.h)
	AC_CHECK_FUNCS(thr_yield)
	AC_SEARCH_LIBS(thr_create,thread,found=yes,found=no)
	if test "$found" != "yes" ; then
	   AC_MSG_ERROR([unable to find Solaris threads library])
	fi
	# FIXME: need to add -mt if using solaris compilers
	;;
    no|none)
	with_thread_package=none
	;;
    *)
	AC_MSG_ERROR([The specified thread package, $with_thread_package, is not supported.]) 
	;;
esac

AC_CHECK_FUNCS(sched_yield yield usleep sleep select)

if test "$with_thread_package" != "none" ; then
    if $CC -V 2>&1 | grep -i "sun workshop" 2>&1 >/dev/null ; then
        # FIXME: the above should really be done using a feature test
        # FIXME: the C++, F77 and F90 compilers also need the -mt flag
	THR_CFLAGS="$THR_CFLAGS -mt"
	THR_LDFLAGS="$THR_LDFLAGS -mt"
    else
	# FIXME: the reentrant flags belong in DEFS, but DEFS doesn't appear to be propagated by MPICH2
	# THR_DEFS="$THR_DEFS -D_REENTRANT -D_THREAD_SAFE"
	THR_CFLAGS="$THR_CFLAGS -D_REENTRANT -D_THREAD_SAFE"
    fi
fi

use_thread_pkg="MPICH_THREAD_PKG_`echo $with_thread_package | \
    tr 'abcdefghijklmnopqrstuvwxyz' 'ABCDEFGHIJKLMNOPQRSTUVWXYZ'`"
AC_DEFINE_UNQUOTED(USE_THREAD_PKG,$use_thread_pkg,[Thread package selected at configure time])

MPE_THREAD_TYPEDEFS="${srcdir}/${with_thread_package}_types.i"
MPE_THREAD_FUNCS="${srcdir}/${with_thread_package}_funcs.i"
AC_SUBST_FILE(MPE_THREAD_TYPEDEFS)
AC_SUBST_FILE(MPE_THREAD_FUNCS)

DEFS="$DEFS $THR_DEFS"
CFLAGS="$CFLAGS $THR_CFLAGS"
LDFLAGS="$LDFLAGS $THR_LDFLAGS"
LIBS="$LIBS -lmpe_thread $THR_LIBS"

AC_SUBST(THR_DEFS)
AC_SUBST(THR_CFLAGS)
AC_SUBST(THR_CPPFLAGS)
AC_SUBST(THR_LDFLAGS)
AC_SUBST(THR_LIBS)

MPE_THREAD_LIB_NAME=${MPILIBNAME}
AC_SUBST(MPE_THREAD_LIB_NAME)

dnl
dnl Copy source files into the build directory when config.status is run
dnl
MPE_THREAD_SOURCE_FILES="mpe_thread.c:$srcdir/mpe_thread_${with_thread_package}.c"
AC_OUTPUT_COMMANDS([
    for entry in $MPE_THREAD_SOURCE_FILES ; do
	destfile="`echo $entry | sed -e 's/:.*$//'`"
	srcfile="`echo $entry | sed -e 's/^.*://'`"
	echo "copying $srcfile to $destfile"
	rm -f $destfile
	cat >$destfile <<END
/*
 * WARNING: DO NOT EDIT!  This file is a copy of $srcfile.
 */

END
	cat $srcfile >>$destfile
	chmod 444 $destfile
    done
],[
    MPE_THREAD_SOURCE_FILES=$MPE_THREAD_SOURCE_FILES
])

dnl FIXME: fixup paths to be configurable so that MPE can also use this package
AC_OUTPUT(Makefile ../../include/mpe_thread.h:mpe_thread.h.in localdefs)

PAC_SUBDIR_CACHE_CLEANUP
