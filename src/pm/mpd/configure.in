dnl Process this file with autoconf to produce a configure script.
# Autoconf initialization:
AC_INIT(mpdroot.h)
AC_PREREQ(2.13)                 dnl needed for SHELL
# Autoconf header:
rm -f ${srcdir}/mpdconf.h mpdconf.h
AC_CONFIG_HEADER(mpdconf.h)

dnl Process configure arguments
AC_ARG_ENABLE(echo,
   [--enable-echo - turn on echoing from within configure script],
   set -x)
CONFIGURE_ARGS="$*"
if test -n "$CONFIGURE_ARGS" ; then 
    echo "Configuring with args $CONFIGURE_ARGS"
fi
AC_SUBST(COPTIONS)
dnl add --enable-strict and --enable-g

AC_ARG_ENABLE(g, [--enable-g  - turn on debugging flags ] )
if test "$enable_g" = "yes" -o "$enable_g" = "all" ; then
    COPTIONS="${COPTIONS} -g"
fi

dnl Checks for programs.
dnl Determine the executable and object file extensions.  These
dnl are needed for some operations under cygwin
AC_EXEEXT
AC_OBJEXT
# Checking for programs.
AC_PROG_CC
AC_PROG_MAKE_SET
AC_PROG_LN_S


dnl Find Python
for pypgm in python2.3 python2 python2.2 python ; do \
    AC_CHECK_PROGS(PYTHON, $pypgm)
    dnl check python version
    AC_CACHE_CHECK([python version],
    pac_cv_python_version,[
    changequote(,)dnl
    if test -z "$PYTHON" ; then
        continue
    fi
    pac_cv_python_version=`$PYTHON -c "import sys, string;print int(string.split(string.split(sys.version,' ')[0],'.')[0])"`
    pac_cv_python_subversion=`$PYTHON -c "import sys, string;print int(string.split(string.split(sys.version,' ')[0],'.')[1])"`
    changequote([,])dnl
    ])
    if test "$pac_cv_python_version" -gt "2" -o "$pac_cv_python_version" -ge "2" -a "$pac_cv_python_subversion" -ge "2" ; then
	break
    fi	       
    PYTHON=""    # not suitable
    echo END LOOP
done
#
if test -z "$PYTHON" ; then
    AC_MSG_ERROR([Python required (version 2.2 or greater)]) 
fi


# Test for xml.dom.minidom module
AC_MSG_CHECKING([that the xml module is available for Python])
rm -f conftest*
cat > conftest.py <<EOF
from xml.dom.minidom import parseString
EOF
if $PYTHON conftest.py >/dev/null 2>&1 ; then
    rm -f conftest*
    AC_MSG_RESULT(yes)
else
    rm -f conftest*
    AC_MSG_RESULT(no)
    AC_MSG_ERROR([The xml.dom.minidom module is required for the mpd process manager])
fi
# In case clinker is not defined
if test -z "$CLINKER" ; then 
    CLINKER="$CC"
    AC_SUBST(CLINKER)
fi

dnl Checks for libraries.
# Searching libraries instead of checking
AC_SEARCH_LIBS(socket, socket)

dnl Checks for header files.
# Checking headers
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS(fcntl.h strings.h sys/time.h unistd.h stdlib.h)

dnl Checks for library functions.
# Checking library functions
AC_CHECK_FUNCS(putenv socket)

dnl Output:
# Starting output
MPD_HOME=`pwd`
AC_SUBST(MPD_HOME)
dnl
dnl Dependency handling
AC_SUBST(MAKE_DEPEND_C)
# AC_OUTPUT_COMMANDS(chmod +x startdaemons)
AC_OUTPUT(Makefile)
