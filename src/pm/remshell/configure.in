dnl Process this file with autoconf to produce a configure script.
dnl
dnl aclocal_cache.m4, included by sowing/confdb/aclocal.m4, fixes 
dnl bugs in autoconf caching.
dnl
dnl The file name here refers to a file in the source being configured
AC_INIT(mpiexec.c)
dnl
dnl Definitions will be placed in this file rather than in the DEFS variable
AC_CONFIG_HEADER(remshellconf.h)
dnl
dnl Set the directory that contains support scripts such as install-sh and
dnl config.guess
AC_CONFIG_AUX_DIR(../../../confdb)
dnl
echo "RUNNING CONFIGURE FOR THE REMOTE SHELL PM"
dnl
dnl Use AC_ARG_ENABLE to look for --enable-feature and AC_ARG_WITH to look for
dnl --with-capability
dnl
dnl Enable better caching control
PAC_ARG_CACHING
dnl
AC_ARG_ENABLE(echo, 
[--enable-echo  - Turn on strong echoing. The default is enable=no.] ,set -x)
dnl
AC_ARG_ENABLE(strict,[--enable-strict - Turn on strict debugging with gcc],,
enable_strict=no)
dnl
dnl
dnl Set single for now
AC_ARG_ENABLE(g,
[--enable-g=option - Control the level of debugging support in the PMI
implementation.  option may be a list of common separated names including
    none  - No debugging
    all   - All of the above choices],,enable_g=none)
AC_ARG_ENABLE(onsig,
[--enable-onsig - Control the handling of processes that signal (e.g., SEGV)
 using ptrace.  Enabled by default],,enable_onsig=yes)
AC_ARG_ENABLE(newsession,
[--enable-newsession - Create a new process group session if standard in is
                      not connected to a terminal],,enable_newsession=yes)
dnl
dnl
dnl First check that we have a clean build if we are doing a VPATH build
if test "`cd $srcdir && pwd`" != "`pwd`" && test -f $srcdir/config.status ; then
    AC_MSG_ERROR([You cannot do a VPATH build if the source directory has been
    configured.  Run "make distclean" in $srcdir first.])
fi
dnl
dnl Process any enable or with values
dnl
dnl enable-g
case "$enable_g" in 
    no|none)
    ;;
    mem|dbg|all|yes)
    CFLAGS="$CFLAGS -g"
    ;;
    *)
    AC_MSG_WARN([Unknown value $enable_g for enable-g])
    ;;
esac
dnl
dnl This test is complicated by the fact that top_srcdir is not set until
dnl the very end of configure.  Instead, we get it ourselves
if test -z "$top_srcdir" ; then
   use_top_srcdir=$srcdir   
else
   use_top_srcdir=$top_srcdir
fi
dnl The recommended order for items in a configure.in file is
dnl     check for programs
dnl     check for libraries
dnl     check for header files
dnl     check for typedefs
dnl     check for structures
dnl     check for compiler characteristics
dnl     check for library functions
dnl     check for system services
dnl Here are some sample tests
dnl
dnl Determine the executable and object file extensions.  These
dnl are needed for some operations under cygwin
AC_EXEEXT
AC_OBJEXT
dnl Find a C compiler (choose gcc first)
AC_PROG_CC
AC_PROG_INSTALL
PAC_PROG_MKDIR_P
PAC_PROG_MAKE
dnl
dnl     check for compiler characteristics
dnl Set CFLAGS for enable strict if necessary
PAC_CC_STRICT
AC_C_CONST
PAC_C_VOLATILE
PAC_C_RESTRICT
AC_C_INLINE
dnl
dnl check for library functions
AC_CHECK_FUNCS(strtol time gettimeofday)
if test "$enable_onsig" = "yes" ; then
    AC_CHECK_FUNCS(ptrace)
    # It isn't enough to find ptrace.  We also need the ptrace 
    # parameters, which some systems, such as IRIX, do not define.
    if test "$ac_cv_func_ptrace" = yes ; then
        AC_CACHE_CHECK([for ptrace named parameters],
pac_cv_has_ptrace_parms,[
        AC_TRY_COMPILE([
#include <sys/types.h>
#include <sys/ptrace.h>],[int i = PTRACE_CONT;],pac_cv_has_ptrace_parms=yes,
pac_cv_has_ptrace_parms=no)])
        if test "$pac_cv_has_ptrace_parms" = "yes" ; then
	    AC_DEFINE(HAVE_PTRACE_CONT,,[Define if ptrace parameters available])
	fi
    fi
fi
# Check for the functions needed to create a new session.
# cygwin has setsid but no getsid (!)
AC_CHECK_FUNCS(getsid setsid isatty)
if test "$enable_newsession" = "yes" ; then
    AC_DEFINE(USE_NEW_SESSION,1,[Define if mpiexec should create a new process group session])
fi
dnl
dnl Check for special compile characteristics
dnl
AC_SEARCH_LIBS(socketpair,socket)
dnl
dnl Look for Standard headers
AC_HEADER_STDC
dnl Check for a specific header
AC_CHECK_HEADERS(sys/types.h signal.h string.h sys/time.h unistd.h stdlib.h sys/socket.h wait.h errno.h)
dnl
dnl Check for functions.  This invokes another test if the function is 
dnl found.  The braces around the second test are essential. 
dnl AC_CHECK_FUNC(setpgrp,[AC_FUNC_SETPGRP])
AC_CHECK_FUNCS(strsignal snprintf)
dnl 
dnl Check for signal handlers
AC_CHECK_FUNCS(sigaction signal sigset)
if test "$ac_cv_func_sigaction" = "yes" ; then
    AC_MSG_CHECKING(for struct sigaction)
    AC_TRY_COMPILE([#include <signal.h>],[
struct sigaction act; sigaddset( &act.sa_mask, SIGINT );],sigaction_ok="yes",sigaction_ok="no")
    AC_MSG_RESULT($sigaction_ok)
fi
dnl
# Decide on the signal handler to use
if test "$ac_cv_func_sigaction" = "yes" -a "$sigaction_ok" = "yes" ; then
    AC_DEFINE(USE_SIGACTION,,[Define if sigaction should be used to set signals])
elif test "$ac_cv_func_signal" = "yes" ; then
    AC_DEFINE(USE_SIGNAL,,[Define if signal should be used to set signals])
fi
dnl
AC_DEFINE(USE_SIGCHLD_HANDLER,,[Define if mpiexec should use sigchild to handle process exit])

AC_PATH_PROG(SSH,ssh)
if test -z "$SSH" ; then
    AC_PATH_PROG(RSH,rsh)
    if test -n "$RSH" ; then
        remshell="$RSH"
    fi
else
    remshell="$SSH"
fi

dnl Setup other replaceable values
MPILIBNAME=${MPILIBNAME:-"mpich"}
AC_SUBST(MPILIBNAME)
AC_SUBST(CC)
AC_SUBST(CFLAGS)
AC_SUBST(CC_SHL)
AC_SUBST(C_LINK_SHL)
AC_SUBST(master_top_srcdir)
dnl
dnl Dependency handling
AC_SUBST(MAKE_DEPEND_C)

eval eval_datadir=$datadir
AC_DEFINE_UNQUOTED(DEFAULT_MACHINES_PATH,"$eval_datadir:.",[Define the search path for machines files])
AC_DEFINE_UNQUOTED(DEFAULT_REMOTE_SHELL,"$remshell",[Define the default remote shell program to use])
dnl
dnl Generate the Makefiles from Makefile.in
AC_OUTPUT(Makefile)
