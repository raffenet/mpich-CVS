AC_INIT(configure.in)

dnl
dnl Set the directory that contains support scripts such as install-sh and
dnl config.guess
dnl
AC_CONFIG_AUX_DIR(../../../confdb)

dnl
dnl Definitions will be placed in this file rather than in the DEFS variable
dnl
AC_CONFIG_HEADER(../../include/mpidi_ch3_conf.h:include/mpidi_ch3_conf.h.in)


echo "RUNNING CONFIGURE FOR DEVICE CH3"

AC_ARG_WITH(device,
[--with-device=name - Specify the communication device for MPICH.],,
with_device=default)
AC_ARG_ENABLE(strict,[--enable-strict - Turn on strict debugging with gcc],
CFLAGS="$CFLAGS -O -Wall -Wstrict-prototypes -Wmissing-prototypes")

dnl Extract the device and channel name from the --with-device argument
if test "$with_device" = "default" ; then
dnl XXX this is wrong!  this should come from the top level configure.
DEVICE=ch3
CHANNEL=tcp
AC_SUBST(DEVICE)
AC_SUBST(CHANNEL)
else
DEVICE=`echo $with_device | sed -e 's/:.*//g'`
AC_SUBST(DEVICE)
changequote(<<,>>)
CHANNEL=`echo $with_device | sed -e 's/[^:]*://g'`
changequote([,])
AC_SUBST(CHANNEL)
fi

if test ! -d $srcdir/channels/$CHANNEL ; then
    AC_MSG_ERROR([Channel $CHANNEL is unknown])
elif test ! -x $srcdir/channels/$CHANNEL/configure ; then
    AC_MSG_ERROR([Channel $CHANNEL has no configure])
fi

dnl AC_PROG_MAKE_SET
PAC_PROG_MAKE

AC_CHECK_HEADERS(				\
    assert.h					\
    limits.h					\
    sys/types.h					\
    sys/uio.h)

AC_SUBST(AR)
AC_SUBST(RANLIB)
AC_SUBST(MPILIBNAME)
AC_SUBST(CC)
AC_SUBST(CFLAGS)
AC_SUBST(CC_SHL)
AC_SUBST(C_LINK_SHL)
AC_SUBST(master_top_srcdir)

PAC_SUBDIR_CACHE
AC_CONFIG_SUBDIRS(channels/$CHANNEL)

AC_OUTPUT_COMMANDS(echo "copying mpidpre.h to src/include" ;
                   cp $topdir/include/mpidpre.h ../../include/,
		   topdir=$srcdir)
AC_OUTPUT_COMMANDS(echo "copying mpidimpl.h to src/include" ;
                   cp $topdir/include/mpidimpl.h ../../include/)
AC_OUTPUT_COMMANDS(echo "copying mpidpost.h to src/include" ;
                   cp $topdir/include/mpidpost.h ../../include/)
AC_OUTPUT_COMMANDS(echo "copying mpid_datatype.h to src/include" ;
                   cp $topdir/../common/datatype/mpid_datatype.h \
		       ../../include/)
AC_OUTPUT_COMMANDS(echo "copying mpid_dataloop.h to src/include" ;
                   cp $topdir/../common/datatype/mpid_dataloop.h \
		       ../../include/)
AC_OUTPUT_COMMANDS(echo "copying gen_dataloop.h to src/include" ;
                   cp $topdir/../common/datatype/gen_dataloop.h \
		       ../../include/)
AC_OUTPUT(Makefile src/Makefile channels/Makefile)
