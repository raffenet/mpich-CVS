dnl Process this file with autoconf to produce a configure script.
dnl
dnl aclocal_cache.m4, included by sowing/confdb/aclocal.m4, fixes 
dnl bugs in autoconf caching.
dnl
dnl The file name here refers to a file in the source being configured
AC_INIT(include/mpitest.h)
AC_CONFIG_HEADER(include/mpitestconf.h)
dnl
AC_CONFIG_AUX_DIR(../../confdb)
dnl
echo "RUNNING CONFIGURE FOR MPI TESTS"
otherlangs=""
AC_ARG_ENABLE(f77,
[--enable-f77 - Turn on Fortran 77 tests (default)],,enable_f77=yes)
dnl
dnl Set the f90 default to the same as MPICH2 (that is, off by default)
AC_ARG_ENABLE(f90,
[--enable-f90 - Turn on Fortran 90 tests (default)],,enable_f90=no)
AC_ARG_ENABLE(cxx,
[--enable-cxx - Turn on C++ tests (default)],,enable_cxx=yes)
PAC_PROG_MAKE
if test -z "$top_build_dir" ; then 
    top_build_dir=`cd ../.. ; pwd`
fi
# The following choices can cause problems if we configure as part of a 
# MPICH2 configure, because the libraries may not yet exist.  In 
# that case, we wait to make these definitions until after the
# tests, since the environment variables for the compilers is set 
# as part of the initial build
if test "$FROM_MPICH2" != yes ; then
    CC=$top_build_dir/bin/mpicc
    FC=$top_build_dir/bin/mpif77
    F90=$top_build_dir/bin/mpif90
    CXX=$top_build_dir/bin/mpicxx
else
    # Remove the libraries, since they will not be needed for the
    # few tests that we do
    saveLIBS=$LIBS
    LIBS=
fi
# Clear the CPPFLAGS, since the MPI configure will have added many items
# that are not required for user MPI programs.
CPPFLAGS=
#
# Note that some versions of autoconf will insist that the compiler 
# produce executables at this point, which is why we must do something
# special for building within MPICH2
AC_C_LONG_DOUBLE
AC_MSG_CHECKING([whether long long available])
AC_TRY_COMPILE(,[long long a;],pac_cv_have_long_long=yes,pac_cv_have_long_long=no)
AC_MSG_RESULT($pac_cv_have_long_long)
if test "$pac_cv_have_long_long" ; then 
    AC_DEFINE(HAVE_LONG_LONG,1,[Define if compiler supports long long])
fi
#
# General headers
AC_CHECK_HEADERS(unistd.h stdarg.h string.h stdio.h stdlib.h memory.h)

# Define functionality that we support
AC_DEFINE(HAVE_MPI_WIN_CREATE,1,[Define if MPI_Win_create is available])

# Now, select the proper compilers for the tests
if test "$FROM_MPICH2" = yes ; then
    if test "$enable_in_place" = yes ; then
        LIBS=$saveLIBS
        CC=$top_build_dir/bin/mpicc
        FC=$top_build_dir/bin/mpif77
        CXX=$top_build_dir/bin/mpicxx
        F90=$top_build_dir/bin/mpif90
    else
        # note that the default definition of bindir is 
	#    '${exec_prefix}/bin'
        # so even if prefix is set, exec prefix is not until
	# the very last moment (i.e., not here). 
	if test "X$exec_prefix" = "XNONE" ; then
	    saveExec_prefix=$exec_prefix
	    exec_prefix=$prefix
	    eval mpibindir=${bindir}
	    exec_prefix=$saveExec_prefix
        else
            eval  mpibindir=${bindir}
	fi
        CC=$mpibindir/mpicc
        FC=$mpibindir/mpif77
        CXX=$mpibindir/mpicxx
        F90=$mpibindir/mpif90
    fi
fi
# Simple tests for which other languages we can handle.  
# Use these only when configuring separate from an MPICH2 build
f77dir="#"
AC_SUBST(f77dir)
# Set a default for the Fortran 77 version of MPI_Offset.
# FIXME: Find a way to get the correct value
F77_MPI_OFFSET="integer*8"
AC_SUBST(F77_MPI_OFFSET)
if test "$FROM_MPICH2" = yes ; then
    if test "$enable_f77" = yes ; then
       # Assume success
        otherlangs="$otherlangs f77"
	f77dir=f77
    fi
elif test "$enable_f77" = yes ; then
    AC_MSG_CHECKING([that we can build MPI programs with Fortran 77])
    rm -f conftest*
    cat >conftest.f <<EOF
        program main
        include 'mpif.h'
        integer ierr
	call mpi_init(ierr)
        call mpi_finalize(ierr)
        end
EOF
    if $FC -o conftest conftest.f >conftest.out 2>&1 ; then
        AC_MSG_RESULT(yes)
        otherlangs="$otherlangs f77"
	f77dir=f77
    else
        AC_MSG_RESULT(no)
    fi
    rm -f conftest*
fi
f90dir="#"
AC_SUBST(f90dir)
if test "$FROM_MPICH2" = yes ; then
    if test "$enable_f90" = yes ; then
        otherlangs="$otherlangs f90"
	f90dir=f90
    fi
elif test "$enable_f90" = yes ; then
    AC_MSG_CHECKING([that we can build MPI programs with Fortran 90])
    rm -f conftest*
    cat >conftest.f90 <<EOF
        program main
        use mpi
        integer ierr
	call mpi_init(ierr)
        call mpi_finalize(ierr)
        end
EOF
    if $F90 -o conftest conftest.f90 >conftest.out 2>&1 ; then
        AC_MSG_RESULT(yes)
        otherlangs="$otherlangs f90"
	f90dir=f90
    else
        AC_MSG_RESULT(no)
	echo "Failed to build program" >&AC_FD_CC
	cat conftest.f90 >&AC_FD_CC
        echo "Error messages:" >&AC_FD_CC
	cat conftest.out >&AC_FD_CC
    fi
    rm -f conftest*
fi
# Simple tests for which other languages we can handle
cxxdir="#"
AC_SUBST(cxxdir)
if test "$FROM_MPICH2" = yes ; then
    if test "$enable_cxx" = yes ; then
        otherlangs="$otherlangs cxx"
	cxxdir=cxx
    fi
elif test "$enable_cxx" = yes ; then
    AC_MSG_CHECKING([that we can build MPI programs with C++])
    rm -f conftest*
    cat >conftest.cxx <<EOF
#include "mpi.h"
int main( int argc, char *argv[])
{
    MPI::Init();
    MPI::Finalize();
    return 0;
}    
EOF
    if $CXX -o conftest conftest.cxx >conftest.out 2>&1 ; then
        AC_MSG_RESULT(yes)
        otherlangs="$otherlangs cxx"
	cxxdir=cxx
    else
        AC_MSG_RESULT(no)
    fi
    rm -f conftest*
fi
# Find perl; used to create some of the tests from template and 
# defintion files
AC_PATH_PROG(PERL,perl)
AC_SUBST(PERL)
AC_SUBST(otherlangs)
AC_SUBST(CC)
AC_SUBST(FC)
AC_SUBST(F90)
AC_SUBST(F90FLAGS)
AC_SUBST(FFLAGS)
AC_SUBST(CXX)
AC_SUBST(CXXFLAGS)
AC_SUBST(MAKE)
AC_SUBST(CFLAGS)
AC_SUBST(CPPFLAGS)
AC_SUBST(LDFLAGS)
if test -z "$MPILIBNAME" ; then MPILIBNAME=mpich ; fi
AC_SUBST(MPILIBNAME)
AC_OUTPUT_COMMANDS([chmod a+x maint/testmerge])
AC_OUTPUT(maint/testmerge \
          Makefile \
          basic/Makefile \
	  attr/Makefile \
	  util/Makefile \
	  coll/Makefile \
	  comm/Makefile \
	  datatype/Makefile \
	  errhan/Makefile \
	  group/Makefile \
	  info/Makefile \
	  init/Makefile \
	  pt2pt/Makefile \
          rma/Makefile \
	  spawn/Makefile \
	  topo/Makefile \
	  io/Makefile \
	  f77/Makefile \
	  f77/attr/Makefile \
	  f77/datatype/Makefile \
	  f77/util/Makefile \
	  f77/coll/Makefile \
	  f77/pt2pt/Makefile \
	  f77/info/Makefile \
	  f77/spawn/Makefile \
	  f77/rma/Makefile \
          f77/io/Makefile \
	  f77/io/iooffset.h \
	  f90/Makefile \
	  f90/misc/Makefile \
	  f90/util/Makefile \
	  f90/attr/Makefile \
	  f90/coll/Makefile \
	  f90/datatype/Makefile \
	  f90/rma/Makefile \
	  cxx/Makefile \
	  cxx/util/Makefile \
	  cxx/coll/Makefile \
	  cxx/io/Makefile \
	  testlist \
	  )
